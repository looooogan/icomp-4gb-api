group 'com.pro4gb'
version '1.0-SNAPSHOT'

subprojects {
    apply plugin: 'java'
    // apply plugin: 'eclipse'
    apply plugin: 'idea'

    version = '1.0'

    // JVM 版本号要求
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // java编译的时候缺省状态下会因为中文字符而失败
    [compileJava,compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

    //定义版本号
    ext {
        springVersion = '4.2.2.RELEASE'
//        hibernateVersion='5.2.2.Final'
    }

    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public" }
        mavenCentral()
        jcenter()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
        maven { url 'http://maven.springframework.org/release' }
        maven { url 'http://maven.springframework.org/milestone' }
    }

    jar {
        manifest {
            attributes("Implementation-Title": "Gradle")
        }
    }

    configurations {
        // 所有需要忽略的包定义在此
        all*.exclude group: 'commons-httpclient'
        all*.exclude group: 'commons-logging'
        all*.exclude group: 'commons-beanutils', module: 'commons-beanutils'
    }

    dependencies {
        // 通用依赖
        compile(
                "org.springframework:spring-webmvc:$springVersion",
                "org.springframework:spring-context:$springVersion",
                "org.springframework:spring-jdbc:$springVersion",
                "org.springframework:spring-context-support:$springVersion",
                "com.alibaba:druid:1.1.9",
                "com.thetransactioncompany:cors-filter:2.6",
                "c3p0:c3p0:0.9.1.2",
                "org.mybatis:mybatis:3.4.6",
                "org.mybatis:mybatis-spring:1.3.2",
                "mysql:mysql-connector-java:8.0.11",
                "javax.validation:validation-api:1.1.0.Final",
                "org.hibernate:hibernate-validator:5.0.2.Final",
                "org.slf4j:jcl-over-slf4j:1.6.4",
                "ch.qos.logback:logback-classic:1.1.1",
                "ch.qos.logback:logback-core:1.1.1",
                "org.logback-extensions:logback-ext-spring:0.1.1",
                "org.apache.commons:commons-lang3:3.3",
                "jstl:jstl:1.2",
                "org.aspectj:aspectjweaver:1.7.4",
                "com.fasterxml.jackson.core:jackson-core:2.4.1.1",
                "com.fasterxml.jackson.core:jackson-databind:2.4.1.1",
                "com.fasterxml.jackson.core:jackson-annotations:2.4.1",
                "org.apache.taglibs:taglibs-standard-impl:1.2.1",
                "javax.mail:mail:1.4.7",
                "commons-codec:commons-codec:1.11",
                "javax.servlet:javax.servlet-api:4.0.1",
                "commons-fileupload:commons-fileupload:1.3.3"


        )

        // 依赖maven中不存在的jar
        ext.jarTree = fileTree(dir: 'libs', include: '**/*.jar')
        ext.rootProjectLibs = new File(rootProject.rootDir, 'libs').getAbsolutePath()
        ext.jarTree += fileTree(dir: rootProjectLibs, include: '**/*.jar')

        compile jarTree

        // 测试依赖
        testCompile(
                "org.springframework:spring-test:$springVersion",
                "junit:junit:4.12"
        )
    }

    // 显示当前项目下所有用于 compile 的 jar.
    task listJars(description: 'Display all compile jars.') << {
        configurations.compile.each { File file -> println file.name }
    }
}
